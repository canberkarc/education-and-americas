pipeline MyEcoPipeline {
    
    // Author: Canberk Arıcı

    MyDataExtractor
        -> MyXLSXInterpreter
        -> MySheetPicker
        -> MyCellRangeSelector
        -> MyColumnRenamer;

    MyColumnRenamer
        -> GDPTableInterpreter
        -> GDPDatabaseLoader;
    
    MyColumnRenamer
        -> BondIssuanceTableInterpreter
        -> BondIssuanceDatabaseLoader;

    // Extractor block to extract data from the given URL
    block MyDataExtractor oftype HttpExtractor { 
        url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx"; 
        retries : 3;
    }

    // Block to interpret the XLSX data
    block MyXLSXInterpreter oftype XLSXInterpreter {}

    // Block to select the sheet of the table as given in the exercise
    block MySheetPicker oftype SheetPicker {
        sheetName: "Figure S5.1.2";
    }

    // Block to select the range of the table as given in the exercise
    block MyCellRangeSelector oftype CellRangeSelector {
        select: range P2:S45;
    }

    // Block to rename the columns of the table
    block MyColumnRenamer oftype CellWriter {
        at: range A1:D1;
        write: [
            'Country Code',
            'Economy',
            'GDP per Capita',
            'Bond Issuance Share'
        ];
    }
    
    // Block to interpret the GDP data as a table
    block GDPTableInterpreter oftype TableInterpreter {
        header: true; 
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "GDP per Capita" oftype PositiveDecimal
        ];
    }

    // Block to interpret the Bond Issuance data as a table
    block BondIssuanceTableInterpreter oftype TableInterpreter {
        header: true; 
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "Bond Issuance Share" oftype ValidBondIssuanceShare
        ];
    }

    // load GDP table into a SQLite database
    block GDPDatabaseLoader oftype SQLiteLoader { 
        table: "gdpPerCapita"; 
        file: "./country-stats.sqlite"; 
    }


    // load Bond Issuance table into a SQLite database
    block BondIssuanceDatabaseLoader oftype SQLiteLoader { 
        table: "bondIssuance"; 
        file: "./country-stats.sqlite"; 
    }
}

// New data type for column "Country Code" which must be a valid ISO 3166-1 alpha-3 country code 
// and must not be empty
valuetype CountryCodeAlpha3 oftype text {
  constraints: [
    ValidCountryCode,
    NotEmpty,  // Ensure the value is not empty
  ];
}

// New data type for positive decimal values
valuetype PositiveDecimal oftype decimal {
  constraints: [
    LowerBoundConstraint
  ];
}

// New data type for Bond Issuance Share which must be a valid decimal value between 0 and 1
valuetype ValidBondIssuanceShare oftype decimal {
  constraints: [
    BoundConstraintBondIssuance
  ];
}

// Regex to match valid ISO 3166-1 alpha-3 country codes
constraint ValidCountryCode on text: value matches /^[A-Z]{3}$/;

// Regex to ensure the value is not empty
constraint NotEmpty on text: value matches /^.+$/;

// Constraint to ensure GDP per Capita is a positive decimal value
constraint LowerBoundConstraint oftype RangeConstraint { 
    lowerBound: 0;
    lowerBoundInclusive: false;
    }

// Constraint to ensure Bond Issuance Share is a decimal value between 0 and 1 (inclusive)
constraint BoundConstraintBondIssuance oftype RangeConstraint { 
    lowerBound: 0;
    lowerBoundInclusive: true;
    upperBound: 1;
    upperBoundInclusive: true;
    }